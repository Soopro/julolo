core = require('core.js')
utils = require('utils.js')
restStore = require('restapi/store.js')


# app
App
  project:
    name: 'Hayangsu Mini'
    version: '1.2.0'
    creator: [
      'Redyyu'
    ]

  # trigger
  onLaunch: (opts)->
    self = @
    console.info 'Hayangsu Launched...'
    console.info '-------------------'
    console.info self.project.name
    console.info self.project.version
    console.info self.project.creator.join(', ')
    if opts.query.api
      core.config.baseURL.api = opts.query.api
    restStore.store.get()
    .then (store)->
      self.splash = store.splash if store.splash

  # global data
  g: {}
  cart: new core.Cart()
  splash: core.config.splash

  # methods
  goto: (opts, auth_required)->
    self = @
    if wx.getStorageSync('auth') or not auth_required
      target_url = opts.route
      joiner = '?'
      for k, v of opts.query
        if k and v
          target_url = target_url + joiner + k + '=' + v
          joiner = '&'

      nav_fn = opts.method or wx.navigateTo
      nav_fn
        url: target_url
    else
      warp_point =
        route: opts.route or ''
        query: opts.query or {}
      wx.setStorageSync('warp_point', warp_point)
      wx.redirectTo
        url: core.config.paths.login

  back: (delta)->
    wx.navigateBack
      delta: delta or 1

  chown: (user_id)->
    auth = wx.getStorageSync('auth') or {}
    return auth.id == user_id and user_id

  get_profile: (callback)->
    self = @
    if not utils.isFunction(callback)
      callback = ->
    if not self.g.profile
      self.g.profile = wx.getStorageSync('profile') or {}
    callback(self.g.profile)

  sync_profile: (callback)->
    self = @
    if not utils.isFunction(callback)
      callback = ->
    core.authorize_run
      scope: 'scope.userInfo'
      required: true
    , (info)->
      wx.getUserInfo
        success: (data)->
          profile = core.make_profile(data.userInfo)
          self.g.profile = profile
          wx.setStorageSync('profile', profile)
          callback(self.g.profile)

  share: ->
    self = @
    share_opts =
      imageUrl: self.splash
    return share_opts


  parse_coupon: (coupon_info)->
    try
      matched = coupon_info.match(/(\d+元)/ig, '')
    catch
      matched = null
    if matched
      return matched[matched.length - 1] + '券'
    else
      return ''

  show_coupon: (opts)->
    wx.setClipboardData
      data: opts.code or '...'
    core.dialog.alert
      title: opts.code
      content: opts.msg

  go_cart: ->
    wx.switchTab
      url: core.config.paths.user

  go_error: ->
    wx.redirectTo
      url: core.config.paths.error

  re_launch: (clear)->
    self = @
    wx.reLaunch
      url: core.config.paths.index


  debug: ->
    user = wx.getStorageSync('auth')
    return if not user.supervisor
    debug = not Boolean(wx.getStorageSync('debug'))
    wx.setEnableDebug
      enableDebug: debug
      success: ->
        wx.setStorageSync('debug', debug)
